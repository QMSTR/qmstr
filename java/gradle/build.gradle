buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
  }
  dependencies {
    // ASSUMES GRADLE 2.12 OR HIGHER. Use plugin version 0.7.5 with earlier
    // gradle versions
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.5'
    classpath 'org.junit.platform:junit-platform-gradle-plugin:1.1.0'
  }
}

repositories {
  maven {
      // The google mirror is less flaky than mavenCentral()
    url "https://maven-central.storage-download.googleapis.com/repos/central/data/"
  }
}

apply plugin: 'com.google.protobuf'
apply plugin: 'java'
sourceCompatibility = 1.8
targetCompatibility = 1.8

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'org.junit.platform.gradle.plugin'

apply plugin: "maven"
group = "org.qmstr.gradle"
version = "0.1"

def grpcVersion = '1.10.0'
def nettyTcNativeVersion = '2.0.7.Final'

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile "com.google.api.grpc:proto-google-common-protos:1.0.0"
  compile "io.grpc:grpc-netty:${grpcVersion}"
  compile "io.grpc:grpc-protobuf:${grpcVersion}"
  compile "io.grpc:grpc-stub:${grpcVersion}"

  testCompile "io.grpc:grpc-testing:${grpcVersion}"

  testCompile("org.junit.jupiter:junit-jupiter-api:5.1.0")
  testRuntime("org.junit.jupiter:junit-jupiter-engine:5.1.0")
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.5.1-1'
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

